// CUP specification for ReactivityEngine parser

package com.pervasa.reactivity;

/* Preliminaries */
parser code {:

  ReactiveEngine re;
  
  public parser(Lexer l, ReactiveEngine re) {
   	this(l);
  	this.re = re;
  }
  
  public void syntax_error(java_cup.runtime.Symbol current) {
    report_error(Syntax.error(current), current);
  }
  public void report_error(String message, java_cup.runtime.Symbol info) {
    re.error(message);
  }
  
:}

/* Terminals (tokens returned by the scanner) */
terminal Integer NUMBER;
terminal String  IDENTIFIER, DATE, TIME;
terminal     	 DEFINE, LIST, BASIC, SET, RUN, STOP, LOAD;
terminal     	 EVENT, CONDITION, ACT, RULE;
terminal     	 EQUALS, TRUE, FALSE, PLUS, MINUS, STAR, PERCENT, NIL;
terminal     	 LPAREN, RPAREN, LBRACKET, RBRACKET, LANGLE, RANGLE;
terminal     	 SEMI, COMMA, SLASH, WHACKWHACK; 

/* Non-terminals */
non terminal cmd, basic, list, define;
non terminal Boolean boolean;
non terminal String act, acts;
non terminal OptEvent event, events, factor;
non terminal Window window;
non terminal EvalFreq eval_freq;

/* Precedences */
precedence left PLUS;
precedence left STAR;
precedence left SEMI;

/* The simplified grammar */
/* The following is for reference purposes only.  It is just
   the grammar, without any code to execute for each rule.
   
cmd	::=
	  basic
	| list
	| define
	| SET IDENTIFIER:i EQUALS boolean:b
	| RUN
	| STOP
	| LOAD IDENTIFIER:i
	;

basic ::= 
	  BASIC
	| BASIC EVENT
	| BASIC ACT
	;
	
list ::=
	  LIST
	| LIST EVENT
	| LIST ACT
	| LIST CONDITION
	| LIST RULE
	;
	 
define ::=
	  DEFINE EVENT IDENTIFIER:i EQUALS events:e
	| DEFINE ACT IDENTIFIER:i EQUALS acts:a
	| DEFINE CONDITION IDENTIFIER:i EQUALS boolean:b
	| DEFINE RULE IDENTIFIER:i EQUALS IDENTIFIER:e COMMA IDENTIFIER:c COMMA IDENTIFIER:a
	;

events ::= 
	  events:e1 PLUS factor:e2
	| factor:e
	;
	

factor ::=
	  factor:e1 STAR NUMBER:n STAR event:e2
	| factor:e1 STAR event:e2
	| event:e
	;
		
event ::=		
	  IDENTIFIER:i LPAREN NUMBER:n RPAREN
	| IDENTIFIER:i LBRACKET NUMBER:n1 COMMA NUMBER:n2 RBRACKET
	| LANGLE window COMMA eval_freq COMMA NUMBER:n RANGLE LPAREN IDENTIFIER:i RPAREN
	| IDENTIFIER:i
	;	
	
window ::=
	  NIL
	| DATE:d1 SLASH TIME:t1 MINUS DATE:d2 SLASH TIME:t2
	| TIME:t1 MINUS TIME:t2
	;

eval_freq ::=
	  NUMBER:n
	;
	
boolean ::=
	  TRUE
	| FALSE
	;

acts ::=
	  acts:s1 SEMI act:s2
	| act:s
	;

	
act ::= 
	  IDENTIFIER:i
	| IDENTIFIER:i LPAREN NUMBER:n RPAREN
	;

*/

/* The Actual Grammar */

cmd	::=
	  basic
	| list
	| define
	| SET IDENTIFIER:i EQUALS boolean:b
		{: parser.re.setCondition(i, b); :}
	| RUN
		{: parser.re.runCommand(); :}
	| STOP
		{: parser.re.stopCommand(); :}
	| LOAD IDENTIFIER:i
		{: parser.re.loadFile(i); :}
	;

basic ::= 
	  BASIC
		{: parser.re.listBasic(); :}
	| BASIC EVENT
		{: parser.re.listBasicEvents(); :}
	| BASIC ACT
		{: parser.re.listBasicActions(); :}
	;
	
list ::=
	 LIST
	 {: parser.re.listAll(); :}
	 |   LIST EVENT
	 {: parser.re.listEvents(); :}
	 |   LIST ACT
	 {: parser.re.listActions(); :}
	 |   LIST CONDITION
	 {: parser.re.listConditions(); :}
	 |   LIST RULE
	 {: parser.re.listRules(); :}
	 ;
	 
define	 ::=
	DEFINE EVENT IDENTIFIER:i EQUALS events:e
	{: 
		if (e == null) {
			// null indicates that an invalid event was specified
			// Therefore: do nothing
		} else {
			parser.re.defineEvent(i, e);
		}
	:}
	|   DEFINE ACT IDENTIFIER:i EQUALS acts:a
	{:
		if (a.matches("")) {
			// Empty string indicates that an invalid action was specified
			// Therefore: do nothing.
		} else {
			parser.re.defineAction(i, a, a);
		}
	:}
	 |   DEFINE CONDITION IDENTIFIER:i EQUALS boolean:b
	{: parser.re.defineCondition(i, b); :}
	 |   DEFINE RULE IDENTIFIER:i EQUALS IDENTIFIER:e COMMA IDENTIFIER:c COMMA IDENTIFIER:a
	{: parser.re.defineRule(i, e, c, a); :}
	 ;

events ::= 
	events:e1 PLUS factor:e2
	{:
		RESULT = null;
		if (e1 == null || e2 == null) {
		} else { 
			RESULT = OptEvent.OR(e1,e2);
		}
	:}
	| factor:e
	{:
		RESULT = null;
		if (e == null) {
		} else { 
			RESULT = e;
		}
	:}
	;
	

factor ::=
	factor:e1 STAR NUMBER:n STAR event:e2
	{:
		RESULT = null;
		if (e1 == null || e2 == null) {
		} else { 
			RESULT = OptEvent.SECS(e1,n,e2);
		}
	:}
	| factor:e1 STAR event:e2
	{:
		RESULT = null;
		if (e1 == null || e2 == null) {
			// null indicates that e2 is an invalid event
			// Therefore: do nothing
		} else {
			RESULT = OptEvent.AND(e1,e2);
		}
	:}
	| event:e
	{:
		RESULT = null;
		if (e == null) {
			// Null indicates that e is an invalid event
			// Therefore: do nothing
		} else {
			RESULT = e;
		}
	:}
	;
		
event ::=		
	IDENTIFIER:i LPAREN NUMBER:n RPAREN
	{:
		RESULT = null;
		if (parser.re.basicEventExists(i)) {
			RESULT = new OptEvent(parser.re.getDevice(i),n);
		} else {
			parser.re.error("Sensor '" + i + "' does not exist.");
		}
	:}
	| IDENTIFIER:i LBRACKET NUMBER:n1 COMMA NUMBER:n2 RBRACKET
	{:
		RESULT = null;
	  	if (parser.re.basicEventExists(i)) {
	  		RESULT = new OptEvent(parser.re.getDevice(i),n1,n2);
	  	} else {
	  		parser.re.error("Sensor '" + i + "' does not exist.");
	  	}
	:}	
	| LANGLE window:w COMMA eval_freq:e COMMA NUMBER:n RANGLE LPAREN event:i RPAREN
	{:
		RESULT = null;
		if (i == null) {
			// do nothing
		} else {
			RESULT = OptEvent.TFM(i,w,e,n);
		}
	:}
	| IDENTIFIER:i
	{:
		RESULT = parser.re.getEvent(i);
	:}
	;	
	
window ::=
	NIL
	{:
		RESULT = new Window();
	:}
	| DATE:d1 SLASH TIME:t1 MINUS DATE:d2 SLASH TIME:t2
	{:
		RESULT = new Window(d1,t1,d2,t2);
	:}
	| TIME:t1 MINUS TIME:t2
	{:
		RESULT = new Window(t1,t2);
	:}
	;

eval_freq ::=
	NUMBER:n
	{:
		RESULT = new EvalFreq(n);
	:}  
	;

boolean ::=
	  TRUE
	{: RESULT = true; :}
	| FALSE
	{: RESULT = false; :}
	;

acts ::=
	acts:s1 SEMI act:s2
	{:
		RESULT = "";
		if (s2.matches("")) {
		} else {
			RESULT = s1 + ";" + s2;
		}
	:}
	|
	act:s
	{:
		RESULT = "";
		if (s.matches("")) {
		} else {
			RESULT = s;
		}
	:}
	;
	
	
act ::= 
	IDENTIFIER:i
	{:
		RESULT = ""; 
		String expansion = parser.re.evaluateAction(i);
		if (expansion.matches("invalid")) {
		} else {
			RESULT = expansion;
		}
	:}
	  |   IDENTIFIER:i LPAREN NUMBER:n RPAREN
	 {:
	 	RESULT = "";
	  	String basicAction = i + "(" + n + ")";
	  	if (parser.re.basicActionExists(i)) {
	  		RESULT = basicAction;
	  	} else {}
	 :}
	  ;

	  